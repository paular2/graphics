<!DOCTYPE HTML> 
<html lang="en"> 
<head>
<title>Hello Terrain </title>
<meta charset="utf-8"> 
</head>

    
<script id="shader-vs" type="x-shader/x-vertex">
    attribute vec3 aVertexNormal;
    attribute vec3 aVertexPosition;
   
    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform mat3 uNMatrix;

    varying vec3 vNormal;
    varying vec3 vPosition;
    
    varying vec3 heightColor;   // Passed to fragment to change color as a factor of height
    
    uniform float heightMinZ;   // The min and max heights to determine the range for colors
    uniform float heightMaxZ;

    void main(void) {
    
    float increment = (heightMaxZ-heightMinZ)/4.0;
    
    heightColor = vec3(0.0,0.0,0.7); // Lowest Points Blue
    
    if(aVertexPosition.z > heightMinZ+increment)
    {
        heightColor = vec3(0.0,0.5,0.0); // Green
    }
    if(aVertexPosition.z > heightMinZ+2.0*increment)
    {
        heightColor = vec3(0.6,0.0,0.0); // Red
    }
    if(aVertexPosition.z > heightMinZ+3.0*increment)
    {
        heightColor = vec3(1.0,1.0,1.0); // Highest Points White
    }
    
    
    vec4 vertexPositionEye4 = uMVMatrix * vec4(aVertexPosition,1);

    vPosition = vertexPositionEye4.xyz / vertexPositionEye4.w;

    vNormal = normalize(uNMatrix * aVertexNormal);

    gl_Position = uPMatrix*uMVMatrix*vec4(aVertexPosition,1);
    }
</script>
  
<script id="shader-fs" type="x-shader/x-fragment">
    precision mediump float;

    uniform vec3 uLightPosition;
    uniform vec3 uAmbientLightColor;
    uniform vec3 uDiffuseLightColor;
    uniform vec3 uSpecularLightColor;
    
    uniform vec3 uKAmbient;
    uniform vec3 uKDiffuse;
    uniform vec3 uKSpecular;
    
    uniform float uShininess;

    varying vec3 vPosition;
    varying vec3 vNormal;
    
    varying vec3 heightColor;
    
    uniform vec4 fogColor;

    void main(void) {
        vec3 vectorToLightSource = normalize(uLightPosition-vPosition);
        vec3 normalEye = normalize(vNormal);
        float diffuseLightWeighting = max(dot(normalEye, vectorToLightSource), 0.0);
        vec3 viewVectorEye = normalize(-vPosition);
        
        float fogCoord = (gl_FragCoord.z/gl_FragCoord.w);
        const float LOG2 = 1.442695;
        float fogDensity = .4;
        float fogFactor = exp2(-fogDensity*fogCoord*fogCoord*LOG2);
        fogFactor = clamp(fogFactor,0.0,1.0);
        
        
        vec3 halfWay = normalize(vectorToLightSource+viewVectorEye); // Blinn Half way vector
        
        float ndoth = max(dot(normalEye,halfWay), 0.0);
    
        float specularLightWeighting = pow(ndoth,uShininess);
        

        vec3 heightDiffuse = uKDiffuse;
        if(uKDiffuse.x*uKDiffuse.y*uKDiffuse.z != 1.0 && uKDiffuse.x+uKDiffuse.y+uKDiffuse.z != 0.0) // Ignoring Wireframe/Edge colors
        {
            heightDiffuse = heightColor;
        }
        
    
        vec4 fragColor = vec4(uAmbientLightColor*uKAmbient
                        +(uDiffuseLightColor*heightDiffuse)*diffuseLightWeighting
                        +(uSpecularLightColor*uKSpecular)*specularLightWeighting,1);
        
        if(fogColor.x == 1.0)
        {
        gl_FragColor = mix(fogColor, fragColor, fogFactor);
        }
        else
        {
        gl_FragColor = fragColor;
        }
        }
</script>

<script src="gl-matrix-min.js"></script> 
<script src="webgl-utils.js"></script> 
<script src="Terrain.js"></script>  
<script src="HelloTerrain.js"></script>   
<body onload="startup();">
 <canvas id="myGLCanvas" width="800" height="800"></canvas>
    <form id="input_form">
        <fieldset>
            <legend>Rendering Parameters</legend>
         <input type="radio" name="primitive" id="wireframe" value="wireframe"> Wireframe
         <input type="radio" name="primitive" id="polygon" id="polygon" value="polygon" checked> Polygon
         <input type="radio" name="primitive" id="wirepoly" value="wirepoly" > Polygon with Edges  
         <input type="checkbox" name="primitive" id="fog" value="fog" checked> Fog 
        </fieldset>
        <fieldset>
        <legend>Controls</legend> -/= Decrease/Increase Speed<br> a/d Roll Left/Right<br> s/w Pitch Up/Down
        </fieldset>
        </form>
</body>

</html>
